; https://en.wikipedia.org/wiki/Backus%E2%80%93Naur_form#Further_examples (CC BY-SA 3.0), altered.
; Note that this currently describes a subset of the supported syntax, though it fully describes its own syntax.
 root           ≔ syntax EOF 
 syntax         ≔ maybe-rule syntax | maybe-rule
 maybe-rule     ≔ comment | rule
 comment        ≔ ";" comment-data line-end
 comment-data   ≔ comment-char comment-data | ""
 comment-char   ≔ letter | digit | symbol | '"' | "'"
 rule           ≔ opt-whitespace rule-name opt-whitespace "≔" opt-whitespace expression line-end
 opt-whitespace ≔ whitespace | ""
 whitespace     ≔ " " | " " whitespace
 expression     ≔ list maybe-expr
 maybe-expr     ≔ whitespace "|" whitespace expression | ""
 line-end       ≔ opt-whitespace EOL | opt-whitespace EOL line-end
 list           ≔ term maybe-list
 maybe-list     ≔ whitespace list | ""
 term           ≔ literal | rule-name
 literal        ≔ '"' text1 '"' | "'" text2 "'"
 text1          ≔ "" | character1 text1
 text2          ≔ '' | character2 text2
 character      ≔ letter | digit | symbol | "≔"
 letter         ≔ "A" | "B" | "C" | "D" | "E" | "F" | "G" | "H" | "I" | "J" | "K" | "L" | "M" | "N" | "O" | "P" | "Q" | "R" | "S" | "T" | "U" | "V" | "W" | "X" | "Y" | "Z" | "a" | "b" | "c" | "d" | "e" | "f" | "g" | "h" | "i" | "j" | "k" | "l" | "m" | "n" | "o" | "p" | "q" | "r" | "s" | "t" | "u" | "v" | "w" | "x" | "y" | "z"
 digit          ≔ "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9"
 symbol         ≔  "|" | " " | "!" | "#" | "$" | "%" | "&" | "(" | ")" | "*" | "+" | "," | "-" | "." | "/" | ":" | ";" | "<" | "=" | ">" | "?" | "@" | "[" | "\" | "]" | "^" | "_" | "`" | "{" | "}" | "~"
 character1     ≔ character | "'"
 character2     ≔ character | '"'
 rule-name      ≔ letter rule-chars | letter
 rule-chars     ≔ rule-char rule-chars | rule-char
 rule-char      ≔ letter | digit | "-"
